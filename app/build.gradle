buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/repo' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.14.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 4
def versionMinor = 1
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

repositories {
    maven { url 'https://maven.fabric.io/repo' }
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")
    }

    buildTypes {
        release {
            minifyEnabled false //TODO turn this on after much more testing
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-butterknife-7.pro'
            proguardFile 'proguard-retrofit-2.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
        }
        debug {
            minifyEnabled false
            versionNameSuffix ".${versionBuild} DEBUG"
        }
    }
}

dependencies {
    //Support libraries
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:preference-v14:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'

    //Play services
    compile 'com.google.android.gms:play-services-location:8.4.0'

    //Other libraries
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.google.code.gson:gson:2.4'

    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'com.squareup.mimecraft:mimecraft:1.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'

    compile 'com.heinrichreimersoftware:material-drawer:2.2.0'
    compile 'com.jpardogo.materialtabstrip:library:1.1.0'

    compile 'com.github.afollestad:inquiry:2.0.1'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.0'
    compile 'jp.wasabeef:recyclerview-animators:1.2.1@aar'
    compile 'com.github.clans:fab:1.6.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.1.0@aar') {
        transitive = true;
    }
}